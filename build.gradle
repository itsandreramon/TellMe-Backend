plugins {
	id "com.heroku.sdk.heroku-gradle" version "1.0.4"
	id 'org.springframework.boot' version '2.2.2.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id "com.diffplug.gradle.spotless" version "3.27.0"
	id 'java'
}

group = 'com.tellme'
version = '0.1'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

task buildZip(type: Zip) {
	from compileJava
	from processResources
	into('lib') {
		from configurations.runtimeClasspath
	}
}

build.configure {
	dependsOn bootJar
	build.mustRunAfter clean
}

dependencies {
	implementation 'com.google.firebase:firebase-admin:6.11.0'


	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

heroku {
	appName = "tellme-backend"
	includes = ["app.jar"]
	includeBuildDir = false
}

spotless {
	java {
		removeUnusedImports()
		googleJavaFormat()
		licenseHeaderFile project.rootProject.file('spotless/copyright.java')
	}
}

task stage(type: Copy, dependsOn: [clean, build]) {
	from jar.archivePath
	into project.rootDir
	rename {
		'app.jar'
	}
}
stage.mustRunAfter(clean)
